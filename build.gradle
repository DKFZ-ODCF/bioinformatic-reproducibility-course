buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.1.5'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.15'
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'org.ajoberstar.git-publish' version '1.0.1'
}

apply plugin: 'com.github.jruby-gradle.base'

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.2.0'
}

asciidoctorj {
    version = '1.5.5'
}

asciidoctor {

    dependsOn jrubyPrepareGems
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepareGems.outputDir
    sourceDir = file("src")
    outputDir = file("build")

    attributes 'source-highlighter': 'coderay'

    resources {
        from("src") {
            include("*.png")
        }
    }

    backends = ["html5", "pdf"]

}

gitPublish {
    // where to publish to (repo must exist)
    repoUri = 'https://github.com/DKFZ-ODCF/bioinformatic-reproducibility-course.git'

    // branch will be created if it doesn't exist
    branch = 'gh-pages'

    // what to publish, this is a standard CopySpec
    contents {
        from 'build/html5'
    }

    // message used when committing changes
    commitMessage = 'Publishing a new page' // defaults to 'Generated by gradle-git-publish'
}
